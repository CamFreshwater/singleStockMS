# Single stock recursive Bayes model with fixed beta, alpha variable over time as simple random walk

model{

## Priors
tau_R ~ dgamma(0.01,0.001)         # observation precision
tauw ~ dgamma(0.01,0.001)          # autoregressive tau term
tauv ~ dgamma(0.01,0.001)          # process precision

alpha[1] ~ dnorm(MuA, TauA)        # prior on initial alpha value

# Can get rid of since beta will be input as data
#beta <- 1/smax
#smax ~ dunif(low,upp)              # put prior on smax instead of beta for simplicity  

for (i in 2:N){
   alpha[i] <- alpha[i-1] + w[i]   # alpha follows random walk
   w[i] ~ dnorm(0,tauw)            # with random error, measured by tauw
}

for (i in 1:N){
   RS[i] <- alpha[i] - beta * S[i] + v[i]   # Ricker model, alpha is log(alpha)
   v[i] ~ dnorm(0, tauv)                    # random error term
   Y[i] <- RS[i] +log(S[i])                 # calculate log R
   R_Obs[i] ~ dlnorm(Y[i],tau_R)            # observation model, note Y is estimated log(R) 
 
                                    # Calculate "bayesian P-value"
   Rep[i] ~ dlnorm(Y[i],tau_R)      # Replicated data set
   Pvalue[i]<-step(Rep[i]-R_Obs[i])	# Probability that replicted data are larger than observered
}

#output variables
sig2 <- 1/tau_R            # observation error        
varv<- 1/tauv              # process error
varw<- 1/tauw              # autoregressive error

}